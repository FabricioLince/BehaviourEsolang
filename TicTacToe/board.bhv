@"loading board.bhv..."
Array = [Array load:"lib/array.bhv"]

Board = $${
  
  State = ${EMPTY=0 X=1 O=-1}
  
  stateToString = &a ?= 
    State:EMPTY -> "_"
    State:X -> "X"
    State:O -> "O"
    
  calcWinner = &&(
    h = {
      // 3 horizontals
      (a/3)
      (a/6/-3)
      (a/-3)
      
      // 3 verticals
      ({a:0 a:3 a:6})
      ({a:1 a:4 a:7})
      ({a:2 a:5 a:8})
      
      // 2 diagonals
      ({a:0 a:4 a:8})
      ({a:2 a:4 a:6})
    }
    
    h*=&a>&a+b
    
    [
      (h>&[a|[a==3 a==-3] b]) ?=
        3*State:X -> State:X
        3*State:O -> State:O
    
      (calcIsFull:a State:EMPTY)
    ]
  )
  calcEmptySlots = &&(
    v=a 
    (0..#v)/&a|(v:a)==State:EMPTY
  )
  calcIsFull = &#(a/&a==State:EMPTY) == 0
  
  oppositeFor = &(a*-1)
  
  new = &&(
    ?(
      (type:Array)~="node"
      @"Array was not loaded " + Array
    )
    $${
    
      clear = &&(
        slots:v = {State:EMPTY}*9
      )
      
      slots = (slots=!Array !clear slots)
      
      
      tostring = &(
        (slots:v)*&(
          (stateToString:a)
          +
          [
            "\n" | b%3 == 2
            " "
          ]
        )
      )>&a+b
      
      set = &&(
        slots:!set
      )
      
      get = &&(
        slots:!get
      )
      
      winner = &&(
        calcWinner:slots:v
      )
      
      isfull = &(
        calcIsFull:slots:v
      )
      
      emptyslots = &(
        calcEmptySlots:slots:v
      )
    }
  )
}